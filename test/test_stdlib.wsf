<?xml version="1.0" ?>
<job id="GetCPUInfo">
<script language="VBScript" src="../stdlib.vbs" />
<script language="VBScript" src="../unittest.vbs" />
<script language="VBScript"><![CDATA[

Option Explicit

UnitTest_Description "test for stdlib.vbs"

Sub TestBindValue
  Dim v
  Bind v, "Hello world."
  AssertEqual "Hello world.", v
End Sub
UnitTest_TestCase "TestBindValue"

Sub TestBindObject
  Dim v
  Bind v, Nothing
  AssertSame Nothing, v
End Sub
UnitTest_TestCase "TestBindObject"

Sub TestBindAtValue
  Dim a(1)
  BindAt a, 0, "Hello world."
  AssertEqual "Hello world.", a(0)
  Assert IsEmpty(a(1))
End Sub
UnitTest_TestCase "TestBindAtValue"

Sub TestBindAtObject
  Dim a(1)
  BindAt a, 0, Nothing
  AssertSame Nothing, a(0)
  Assert IsEmpty(a(1))
End Sub
UnitTest_TestCase "TestBindAtObject"

Sub TestListBuffer
  Dim listBuf
  Set listBuf = New ListBuffer

  listBuf.Add "foo"
  listBuf.Add "bar"
  listBuf.Add Nothing

  AssertEqual 3, listBuf.Count

  AssertEqual "foo", listBuf(0)
  AssertEqual "bar", listBuf(1)
  AssertSame Nothing, listBuf(2)

  Dim a
  a = listBuf.Items
  AssertEqual "Variant()", TypeName(a)
  AssertEqual 0, LBound(a)
  AssertEqual 2, UBound(a)
  AssertEqual "foo", a(0)
  AssertEqual "bar", a(1)
  AssertSame Nothing, a(2)

  Dim k
  k = listBuf.Keys
  AssertEqual "Variant()", TypeName(k)
  AssertEqual 0, LBound(k)
  AssertEqual 2, UBound(k)
  AssertEqual 0, k(0)
  AssertEqual 1, k(1)
  AssertEqual 2, k(2)
End Sub
UnitTest_TestCase "TestListBuffer"

UnitTest_ConsoleRun

]]></script>
</job>

<!-- Local Variables: -->
<!-- mode: Visual-Basic -->
<!-- indent-tabs-mode: nil -->
<!-- End: -->

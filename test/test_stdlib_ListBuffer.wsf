<?xml version="1.0" ?>
<job id="test_stdlib_ListBuffer">
<script language="VBScript" src="../stdlib.vbs" />
<script language="VBScript" src="../unittest.vbs" />
<script language="VBScript"><![CDATA[

Option Explicit

UnitTest_Description "stdlib.vbs: ListBuffer class test."

Dim listBuf

Sub SetUp
  Set listBuf = New ListBuffer
End Sub
UnitTest_SetUp "SetUp"

Sub TearDown
  Set listBuf = Nothing
End Sub
UnitTest_TearDown "TearDown"

Sub TestListBuffer_Count
  listBuf.Add "foo"
  listBuf.Add "bar"
  listBuf.Add Nothing

  AssertEqual 3, listBuf.Count
End Sub
UnitTest_TestCase "TestListBuffer_Count"

Sub TestListBuffer_CountEmpty
  AssertEqual 0, listBuf.Count
End Sub
UnitTest_TestCase "TestListBuffer_CountEmpty"

Sub TestListBuffer_GetItem
  listBuf.Add "foo"
  listBuf.Add "bar"
  listBuf.Add Nothing

  AssertEqual "foo", listBuf(0)
  AssertEqual "bar", listBuf(1)
  AssertSame Nothing, listBuf(2)
End Sub
UnitTest_TestCase "TestListBuffer_GetItem"

Sub TestListBuffer_GetItemOutOfRange
  listBuf.Add "foo"
  listBuf.Add "bar"
  listBuf.Add Nothing

  Dim errNum
  On Error Resume Next
  listBuf(3)
  errNum = Err.Number: Err.Clear
  On Error GoTo 0

  AssertEqual 9, errNum
End Sub
UnitTest_TestCase "TestListBuffer_GetItemOutOfRange"

Sub TestListBuffer_GetItemEmpty
  Dim errNum
  On Error Resume Next
  listBuf(0)
  errNum = Err.Number: Err.Clear
  On Error GoTo 0

  AssertEqual 9, errNum
End Sub
UnitTest_TestCase "TestListBuffer_GetItemEmpty"

Sub TestListBuffer_LetItem
  listBuf.Add "foo"
  listBuf.Add "bar"
  listBuf.Add Nothing

  listBuf(2) = "baz"
  AssertEqual "foo", listBuf(0)
  AssertEqual "bar", listBuf(1)
  AssertEqual "baz", listBuf(2)
End Sub
UnitTest_TestCase "TestListBuffer_LetItem"

Sub TestListBuffer_LetItemOutOfRange
  listBuf.Add "foo"
  listBuf.Add "bar"
  listBuf.Add Nothing

  Dim errNum
  On Error Resume Next
  listBuf(3) = "baz"
  errNum = Err.Number: Err.Clear
  On Error GoTo 0

  AssertEqual 9, errNum
End Sub
UnitTest_TestCase "TestListBuffer_LetItemOutOfRange"

Sub TestListBuffer_LetItemEmpty
  Dim errNum
  On Error Resume Next
  listBuf(0) = "foo"
  errNum = Err.Number: Err.Clear
  On Error GoTo 0

  AssertEqual 9, errNum
End Sub
UnitTest_TestCase "TestListBuffer_LetItemEmpty"

Sub TestListBuffer_SetItem
  listBuf.Add "foo"
  listBuf.Add "bar"
  listBuf.Add Nothing

  Set listBuf(0) = Nothing
  AssertSame Nothing, listBuf(0)
  AssertEqual "bar", listBuf(1)
  AssertSame Nothing, listBuf(2)
End Sub
UnitTest_TestCase "TestListBuffer_SetItem"

Sub TestListBuffer_SetItemOutOfRange
  listBuf.Add "foo"
  listBuf.Add "bar"
  listBuf.Add Nothing

  Dim errNum
  On Error Resume Next
  Set listBuf(3) = Nothing
  errNum = Err.Number: Err.Clear
  On Error GoTo 0

  AssertEqual 9, errNum
End Sub
UnitTest_TestCase "TestListBuffer_SetItemOutOfRange"

Sub TestListBuffer_SetItemEmpty
  Dim errNum
  On Error Resume Next
  Set listBuf(0) = Nothing
  errNum = Err.Number: Err.Clear
  On Error GoTo 0

  AssertEqual 9, errNum
End Sub
UnitTest_TestCase "TestListBuffer_SetItemEmpty"

Sub TestListBuffer_Items
  listBuf.Add "foo"
  listBuf.Add "bar"
  listBuf.Add Nothing

  Dim a
  a = listBuf.Items
  AssertEqual "Variant()", TypeName(a)
  AssertEqual 0, LBound(a)
  AssertEqual 2, UBound(a)
  AssertEqual "foo", a(0)
  AssertEqual "bar", a(1)
  AssertSame Nothing, a(2)
End Sub
UnitTest_TestCase "TestListBuffer_Items"

Sub TestListBuffer_ItemsEmpty
  Dim a
  a = listBuf.Items
  AssertEqual "Variant()", TypeName(a)
  AssertEqual 0, LBound(a)
  AssertEqual -1, UBound(a)
End Sub
UnitTest_TestCase "TestListBuffer_ItemsEmpty"

Sub TestListBuffer_Keys
  listBuf.Add "foo"
  listBuf.Add "bar"
  listBuf.Add Nothing

  Dim k
  k = listBuf.Keys
  AssertEqual "Variant()", TypeName(k)
  AssertEqual 0, LBound(k)
  AssertEqual 2, UBound(k)
  AssertEqual 0, k(0)
  AssertEqual 1, k(1)
  AssertEqual 2, k(2)
End Sub
UnitTest_TestCase "TestListBuffer_Keys"

Sub TestListBuffer_KeysEmpty
  Dim k
  k = listBuf.Keys
  AssertEqual "Variant()", TypeName(k)
  AssertEqual 0, LBound(k)
  AssertEqual -1, UBound(k)
End Sub
UnitTest_TestCase "TestListBuffer_KeysEmpty"

Sub TestListBuffer_Exists
  listBuf.Add "foo"
  listBuf.Add "bar"
  listBuf.Add Nothing

  Assert listBuf.Exists(0)
  Assert listBuf.Exists(1)
  Assert listBuf.Exists(2)
  Assert Not listBuf.Exists(3)
End Sub
UnitTest_TestCase "TestListBuffer_Exists"

Sub TestListBuffer_ExistsEmpty
  Assert Not listBuf.Exists(0)
End Sub
UnitTest_TestCase "TestListBuffer_ExistsEmpty"

Sub TestListBuffer_RemoveAll
  listBuf.Add "foo"
  listBuf.Add "bar"
  listBuf.Add Nothing

  listBuf.RemoveAll
  AssertEqual 0, listBuf.Count
  Assert Not listBuf.Exists(0)
  Assert Not listBuf.Exists(1)
  Assert Not listBuf.Exists(2)
End Sub
UnitTest_TestCase "TestListBuffer_RemoveAll"

Sub TestListBuffer_RemoveAllEmpty
  listBuf.RemoveAll
  AssertEqual 0, listBuf.Count
  Assert Not listBuf.Exists(0)
  Assert Not listBuf.Exists(1)
  Assert Not listBuf.Exists(2)
End Sub
UnitTest_TestCase "TestListBuffer_RemoveAllEmpty"

UnitTest_ConsoleRun

]]></script>
</job>

<!-- Local Variables: -->
<!-- mode: Visual-Basic -->
<!-- indent-tabs-mode: nil -->
<!-- End: -->

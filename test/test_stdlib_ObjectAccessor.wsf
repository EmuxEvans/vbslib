<?xml version="1.0" ?>
<job id="test_stdlib_Bind">
<script language="VBScript" src="../stdlib.vbs" />
<script language="VBScript" src="../unittest.vbs" />
<script language="VBScript"><![CDATA[

Option Explicit

UnitTest_Description "stdlib.vbs: Object Accessor test."

Class Foo
  Private ivar_bar

  Public Property Get Bar
    Bind Bar, ivar_bar
  End Property

  Public Property Let Bar(value)
    ivar_bar = value
  End Property

  Public Property Set Bar(value)
    Set ivar_bar = value
  End Property
End Class

Class Bar
End Class

Dim obj_foo, obj_bar

Sub SetUp
  Set obj_foo = New Foo
  Set obj_bar = New Bar
End Sub
UnitTest_SetUp "SetUp"

Sub TearDown
  Set obj_foo = Nothing
  Set obj_bar = Nothing
End Sub
UnitTest_TearDown "TearDown"

Sub TestGetObjectProeprty_Value
  obj_foo.Bar = "Hello world."
  AssertEqual "Hello world.", GetObjectProperty(obj_foo, "Bar")
End Sub
UnitTest_TestCase "TestGetObjectProeprty_Value"

Sub TestGetObjectProeprty_Object
  Set obj_foo.Bar = obj_bar
  AssertSame obj_bar, GetObjectProperty(obj_foo, "Bar")
End Sub
UnitTest_TestCase "TestGetObjectProeprty_Object"

Sub TestSetObjectProperty_Value
  SetObjectProperty obj_foo, "Bar", "Hello world."
  AssertEqual "Hello world.", obj_foo.Bar
End Sub
UnitTest_TestCase "TestSetObjectProperty_Value"

Sub TestSetObjectProperty_Object
  SetObjectProperty obj_foo, "Bar", obj_bar
  AssertSame obj_bar, obj_foo.Bar
End Sub
UnitTest_TestCase "TestSetObjectProperty_Object"

Sub TestExistsObjectProperty_Exists
  AssertEqual True, ExistsObjectProperty(obj_foo, "Bar")
End Sub
UnitTest_TestCase "TestExistsObjectProperty_Exists"

Sub TestExistsObjectProperty_NotExists
  AssertEqual False, ExistsObjectProperty(obj_foo, "Baz")
End Sub
UnitTest_TestCase "TestExistsObjectProperty_NotExists"

UnitTest_ConsoleRun

]]></script>
</job>

<!-- Local Variables: -->
<!-- mode: Visual-Basic -->
<!-- indent-tabs-mode: nil -->
<!-- End: -->
